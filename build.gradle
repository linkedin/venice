if (project.hasProperty('overrideBuildEnvironment')) {
  if(project.overrideBuildEnvironment) {
    apply from: file(project.overrideBuildEnvironment)
    product {
      codeQuality {
        ignoreFailures = true
      }
    }
  }
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'eclipse'

ext.libraries = [ // Groovy map literal
                  avro: 'org.apache.avro:avro:1.4.1',
                  bdbJe: 'com.sleepycat:je:5.0.104',
                  commonsCodec: 'commons-codec:commons-codec:1.4',
                  commonsIo: 'commons-io:commons-io:1.4',
                  guava: 'com.google.guava:guava:18.0',
                  helix: 'org.apache.helix:helix-core:0.6.5',
                  kafka: 'kafka:kafka_2.10:0.9.0.2', // Internal LinkedIn release
                  kafkaClients: 'kafka:kafka-clients:0.9.0.2', // Internal LinkedIn release
                  netty: 'io.netty:netty-all:4.0.33.Final',
                  netty3: 'io.netty:netty:3.9.7.Final',
                  zkclient: 'com.101tec:zkclient:0.7', //for Kafka AdminUtils
                  d2: 'com.linkedin.container:pegasus-d2-server-factory:16.0.23',

                  routerApi: 'com.linkedin.dds-storage-core:dds-storage-router-api:0.0.60',
                  routerImpl: 'com.linkedin.dds-storage-core:dds-storage-router-impl:0.0.60',
                  asyncHttpClient: 'org.apache.httpcomponents:httpasyncclient:4.0.1',
                  zookeeper: 'org.apache.zookeeper:zookeeper:3.4.6',

                  // Hadoop to Venice Bridge dependencies
                  mapreduceClientCore: 'org.apache.hadoop:hadoop-mapreduce-client-core:2.3.0',
                  commonsConfiguration: 'commons-configuration:commons-configuration:1.9',
                  commonsLang: 'commons-lang:commons-lang:2.6',
                  hadoopCommon: 'org.apache.hadoop:hadoop-common:2.3.0',
                  httpClient: 'org.apache.httpcomponents:httpclient:4.2.1',
                  httpCore: 'org.apache.httpcomponents:httpcore:4.2.1',
                  jacksonCore: 'org.codehaus.jackson:jackson-core-asl:1.8.5',
                  jacksonMapper: 'org.codehaus.jackson:jackson-mapper-asl:1.8.5',
                  jdom: 'org.jdom:jdom:1.1',
                  joptSimple: 'net.sf.jopt-simple:jopt-simple:3.2',
                  log4j: 'log4j:log4j:1.2.17',
                  metricsCore: 'com.yammer.metrics:metrics-core:2.2.1',
                  snappy: 'org.xerial.snappy:snappy-java:1.0.4.1',
                  testng: 'org.testng:testng:6.1.1',

                  // Hadoop to Venice Bridge test dependencies
                  mapreduceClientJobClient: 'org.apache.hadoop:hadoop-mapreduce-client-jobclient:2.3.0'
]

subprojects {
  apply plugin: 'java'
  apply plugin: 'idea'
  apply plugin: 'eclipse'

  sourceCompatibility = 1.8
  if (!project.hasProperty('overrideBuildEnvironment') ||
     !project.overrideBuildEnvironment) {
     version = '0.1'
  }
  repositories {
    mavenCentral()
    maven {
      url "http://download.oracle.com/maven/"
    }
    ivy {  //TODO: remove this before opensourcing
      url 'http://artifactory.corp.linkedin.com:8081/artifactory/release/'
      layout "pattern", {
        ivy '[organisation]/[module]/[revision]/[module]-[revision].ivy'
        artifact '[organisation]/[module]/[revision]/[artifact]-[revision](-[classifier]).[ext]'
        m2compatible = true
      }
    }
  }

  dependencies {
    compile 'org.slf4j:slf4j-log4j12:1.7.14'
    compile 'javax.validation:validation-api:1.0.0.GA'
    testCompile 'org.testng:testng:6.1.1'

    testCompile 'org.mockito:mockito-all:1.9.5'
  }

  idea {
    module {
      downloadJavadoc = true
      downloadSources = true
    }
  }

  test {
    // Tests should run in parallel
    maxParallelForks 10
  }

  // Add Logging on what is running and passed, Sometimes the test hangs
  // and is not possible to debug what is going on.
  // uncomment to enable - bloats log output if we don't need it.
/*
  tasks.withType(Test) {
    testLogging {
      events "started", "passed", "skipped", "failed"
      exceptionFormat = 'full'
    }
  }
*/
}

task buildAll {
  dependsOn 'venice-common:build'
  dependsOn 'venice-client:build'
  dependsOn 'venice-thin-client:build'
  dependsOn 'venice-thin-client:test'
  dependsOn 'venice-server:build'
  dependsOn 'venice-controller:build'
  dependsOn 'venice-router:build'
  dependsOn 'hadoop-to-venice-bridge:build'

  dependsOn 'venice-common:buildJar'
  dependsOn 'venice-client:installApp'
  dependsOn 'venice-server:installApp'
  dependsOn 'venice-controller:installApp'
  dependsOn 'venice-router:installApp'
  // dependsOn 'hadoop-to-venice-bridge:installApp'
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.10'
}
