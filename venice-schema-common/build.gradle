task buildJar(type: Jar) {
    manifest {
        attributes 'Implementation-Title': 'Venice Schema Common',
                'Implementation-Version': version
    }
    baseName = project.name
    from { configurations.compile
            .filter {!it.absolutePath.endsWith(".xml")}
            .collect { it.isDirectory() ? it : zipTree(it) }
    }
    with jar
}

jar {
    manifest {
        attributes 'Implementation-Title': 'Venice Schema Common',
                'Implementation-Version': version
    }
}

dependencies {
    // Let's try to keep it clean! (i.e.: alphabetical)
    compile (libraries.avro) {
        exclude group: 'org.jboss.netty' // older version of netty3 causes transitive conflicts with the router
        exclude group: 'org.mortbay.jetty' // jetty 6 conflicts with spark-java used in controller api
        exclude group: 'org.slf4j'        // Avro pulls in slf4j 1.5 which is API incompatible with 1.6
    }

    compile (libraries.d2Client) {
        exclude group: 'com.oracle', module: 'ojdbc14' /* unused transitive dependencies, doesn't exist in repo */
        exclude group: 'org.mockito', module: 'mockito-all'
        exclude group: 'xerces', module: 'xerces'
        exclude group: 'org.codehaus.jackson'
        exclude module: 'httpcore'
        exclude module: 'httpclient'
        exclude group: 'kafka'
    }

    compile libraries.jacksonMapper
    compile libraries.httpClient //d2Client brings in an old version of httpClient
    compile libraries.httpCore  //d2Client brings in an old version of httpCore
    compile libraries.tehuti
    compile libraries.xerces // d2Client brings in an old version of xerces

    testCompile project(':venice-test-common')
}
