apply plugin: 'application'

mainClassName = "com.linkedin.venice.router.RouterServer"

installDist.dependsOn test

task buildJar(type: Jar) {
    zip64 true
    manifest {
        attributes 'Main-Class': 'com.linkedin.venice.router.RouterServer',
            'Implementation-Title': 'Venice Router',
            'Implementation-Version': version
    }
    baseName = project.name
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

jar {
    manifest {
        attributes 'Main-Class': 'com.linkedin.venice.router.RouterServer',
            'Implementation-Title': 'Venice Router',
            'Implementation-Version': version
    }
}

dependencies {
    compile libraries.routerApi
    compile libraries.routerImpl
    compile libraries.routerNetty4
    compile (libraries.netty4Lnkd){
        exclude group: 'javax.servlet'
    }
    compile (libraries.routerLnkd){ // for SSL Initializer
        exclude group: 'com.oracle', module: 'ojdbc14' // unused transitive dependency, doesnt exist in repo

        exclude group: 'xerces', module: 'xerces' // resolve conflicting transitive dependency
        exclude group: 'com.fasterxml.jackson.core', module: 'jackson-core'// resolve conflicting transitive dependency
    }
    compile libraries.asyncHttpClient
    compile project(':venice-common')
    compile (libraries.d2){
        exclude group: 'com.oracle', module: 'ojdbc14' // unused transitive dependencies, doesnt exist in repo
        // this will introduce another different mockito-all version
        exclude group: 'org.mockito', module: 'mockito-all'
    }

    testCompile project(':venice-router')
    testCompile project(':venice-thin-client')
    testCompile project(':venice-brooklin')
}

configurations.all {
    // Router dependencies import from "org.jboss.netty" and we want them to use "io.netty"
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        if (details.requested.group == "org.jboss.netty")
        {
            details.useTarget group: "io.netty", name: details.requested.name, version: "3.9.6.Final"
        }
    }

}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

test {
    useTestNG()
}
