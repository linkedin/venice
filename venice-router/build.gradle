apply plugin: 'application'

mainClassName = 'com.linkedin.venice.router.RouterServer'

installDist.dependsOn test

task buildJar(type: Jar) {
    zip64 true
    manifest {
        attributes 'Main-Class': 'com.linkedin.venice.router.RouterServer',
            'Implementation-Title': 'Venice Router',
            'Implementation-Version': version
    }
    baseName = project.name
    from { configurations.implementation.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

jar {
    manifest {
        attributes 'Main-Class': 'com.linkedin.venice.router.RouterServer',
            'Implementation-Title': 'Venice Router',
            'Implementation-Version': version
    }
}

dependencies {
    implementation libraries.routerApi
    implementation libraries.routerImpl
    implementation libraries.ddsNettyBase
    implementation (libraries.netty4Lnkd) {
        exclude group: 'javax.servlet'
        exclude group: 'org.mockito', module: 'mockito-all'
    }
    implementation (libraries.routerLnkd) { // for SSL Initializer
        exclude group: 'com.oracle', module: 'ojdbc14' // unused transitive dependency, doesnt exist in repo
        exclude group: 'xerces', module: 'xerces' // resolve conflicting transitive dependency
        exclude group: 'com.fasterxml.jackson.core', module: 'jackson-core'// resolve conflicting transitive dependency
    }
    implementation libraries.asyncHttpClient
    implementation project(':venice-common')
    implementation (libraries.d2) {
        exclude group: 'com.oracle', module: 'ojdbc14' // unused transitive dependencies, doesnt exist in repo
        exclude group: 'org.mockito', module: 'mockito-all' // this will introduce another different mockito-all version
    }

    implementation (libraries.helix) { // TODO: Get rid of Helix dependency in venice-common
        // Helix pulls in an ancient version of the zkclient, which lacks some APIs and clashes with Kafka's dependency
        exclude group: 'com.github.sgroschupf'
        // Helix use zk 3.6.9, which introduce netty 3.10 and will fail our test.
        exclude module: 'zookeeper'
    }

    testImplementation project(':venice-thin-client')
    testImplementation libraries.kafkaClientsTest // TODO: Get rid of Kafka dependency in venice-common (used by TopicCreator)
    testImplementation libraries.kafkaClients
}

configurations.all {
    // Router dependencies import from "org.jboss.netty" and we want them to use "io.netty"
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        if (details.requested.group == 'org.jboss.netty') {
            details.useTarget group: 'io.netty', name: details.requested.name, version: '3.9.6.Final'
        }
    }
}
