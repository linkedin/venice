apply plugin: 'application'

mainClassName = "com.linkedin.venice.controller.VeniceController"

installDist.dependsOn test

//the mortbay jetty will introduce servlet 2.5 which will cause the NoSuchMethodException while admin spark server is trying to read head from request.
configurations.compile {
  exclude group: 'org.mortbay.jetty', module:'servlet-api'
}

task buildJar(type: Jar) {
  zip64 true
  manifest {
    attributes 'Main-Class': 'com.linkedin.venice.controller.VeniceController',
        'Implementation-Title': 'Venice Controller',
        'Implementation-Version': version
  }
  baseName = project.name
  from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
  with jar
}

jar {
  manifest {
    attributes 'Main-Class': 'com.linkedin.venice.controller.VeniceController',
        'Implementation-Title': 'Venice Controller',
        'Implementation-Version': version
  }
}

dependencies {

  compile libraries.zkclient // It's necessary to pull in the most recent version of zkclient explicitly, otherwise Helix won't have it...
  compile libraries.kafka

  compile (libraries.brooklinKafkaTransport) {
    exclude group: 'org.apache.kafka'
  }
  compile (libraries.brooklinKafka) {
    exclude group : 'org.apache.kafka'
  }
  compile libraries.brooklinCommon

  compile project(':venice-common')

  compile 'commons-io:commons-io:2.4'
  compile 'com.sleepycat:je:5.0.104'
  compile 'com.sparkjava:spark-core:2.5.5'

  compile (libraries.d2){
    exclude group: 'com.oracle', module: 'ojdbc14' // unused transitive dependencies, doesnt exist in repo
    // this will introduce another different mockito-all version
    exclude group: 'org.mockito', module: 'mockito-all'
  }

  testCompile project(':venice-server')
  testCompile project(':venice-router')
  testCompile project(':venice-brooklin')
  testCompile 'commons-io:commons-io:2.4'

}

