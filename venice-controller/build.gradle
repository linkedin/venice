apply plugin: 'application'

mainClassName = "com.linkedin.venice.controller.VeniceController"

installApp.dependsOn test

task buildJar(type: Jar) {
  manifest {
    attributes 'Main-Class': 'com.linkedin.venice.controller.VeniceController',
        'Implementation-Title': 'Venice Controller',
        'Implementation-Version': version
  }
  baseName = project.name
  from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
  with jar
}

jar {
  manifest {
    attributes 'Main-Class': 'com.linkedin.venice.controller.VeniceController',
        'Implementation-Title': 'Venice Controller',
        'Implementation-Version': version
  }
}

dependencies {

  compile(libraries.helix) {
    // Helix pulls in an ancient version of the zkclient, which lacks some APIs and clashes with Kafka's dependency
    exclude group: 'com.github.sgroschupf'
  }
  compile libraries.zkclient // It's necessary to pull in the most recent version of zkclient explicitly, otherwise Helix won't have it...
  compile libraries.kafka
  compile project(':venice-common')

  compile 'commons-io:commons-io:2.4'
  compile 'com.sleepycat:je:5.0.104'
  compile 'com.sparkjava:spark-core:2.3'
  //compile fileTree(dir: 'lib', include: '*.jar')

  testCompile project(':venice-client')
  //Controller's unit test depends on ZkWrapper which is in tests in project venice-common
  testCompile project(':venice-common').sourceSets.test.output
  testCompile 'commons-io:commons-io:2.4'
}


idea {
  module {
    downloadJavadoc = true
    downloadSources = true
  }
}

test {
  useTestNG()
}
