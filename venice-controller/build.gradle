apply plugin: 'application'

mainClassName = 'com.linkedin.venice.controller.VeniceController'

installDist.dependsOn test

//the mortbay jetty will introduce servlet 2.5 which will cause the NoSuchMethodException while admin spark server is trying to read head from request.
configurations.implementation {
  exclude group: 'org.mortbay.jetty', module: 'servlet-api'
}

task buildJar(type: Jar) {
  zip64 true
  manifest {
    attributes 'Main-Class': 'com.linkedin.venice.controller.VeniceController',
        'Implementation-Title': 'Venice Controller',
        'Implementation-Version': archiveVersion
  }
  archiveBaseName = project.name
  from { configurations.implementation.collect { it.isDirectory() ? it : zipTree(it) } }
  with jar
}

jar {
  manifest {
    attributes 'Main-Class': 'com.linkedin.venice.controller.VeniceController',
        'Implementation-Title': 'Venice Controller',
        'Implementation-Version': archiveVersion
  }
}

dependencies {
  implementation libraries.zkclient // It's necessary to pull in the most recent version of zkclient explicitly, otherwise Helix won't have it...
  implementation libraries.kafka

  implementation project(':venice-thin-client')
  implementation project(':venice-common')

  implementation libraries.commonsIo
  implementation libraries.spark

  implementation (libraries.d2) {
    exclude group: 'com.oracle', module: 'ojdbc14' // unused transitive dependencies, doesnt exist in repo
    exclude group: 'org.mockito', module: 'mockito-all' // this will introduce another different mockito-all version
  }

  implementation (libraries.helix) { // TODO: Get rid of Helix dependency in venice-common
    // Helix pulls in an ancient version of the zkclient, which lacks some APIs and clashes with Kafka's dependency
    exclude group: 'com.github.sgroschupf'
    // Helix use zk 3.6.9, which introduce netty 3.10 and will fail our test.
    exclude module: 'zookeeper'
  }

  testImplementation project(':venice-router')
  testImplementation libraries.avroUtilFastserde
  testImplementation libraries.kafkaClientsTest // TODO: Get rid of Kafka dependency in venice-common (used by TopicCreator)
}
