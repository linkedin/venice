apply plugin: 'java'

jar {
    zip64 true
    manifest {
        attributes 'Implementation-Title': 'Venice Admin Tool',
            'Implementation-Version': version,
            'Main-Class': 'com.linkedin.venice.AdminTool'
    }
    baseName = project.name
    from { configurations.compile
        .filter {!it.absolutePath.endsWith(".xml")} // Resolves Could not expand ZIP '.../pegasus-d2-client-factory-cmpt-def-16.0.23.xml'.
        .filter {!it.absolutePath.endsWith(".ivy")} // Exclude the ivy file which is introduced by new helix version.
        .collect { it.isDirectory() ? it : zipTree(it) }
    }
    exclude 'META-INF/*.RSA', 'META-INF/*.SF','META-INF/*.DSA'
}


dependencies {
    compile project(':venice-common_2.10')
    compile(project(':venice-test-common_2.10')) {
      // Used by the command that spins up a MirrorMaker
      transitive = false
    }

    compile project(':venice-thin-client')
    compile (libraries.avro) {
      exclude group: 'org.mortbay.jetty' // jetty 6 conflicts with spark-java used in controller api
    }

    compile project(':venice-controller')
}
