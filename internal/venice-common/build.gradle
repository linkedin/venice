dependencies {
  api project(':internal:venice-client-common')

  implementation (libraries.avro) {
    exclude group: 'org.jboss.netty' // older version of netty3 causes transitive conflicts with the router
    exclude group: 'org.mortbay.jetty' // jetty 6 conflicts with spark-java used in controller api
    exclude group: 'org.slf4j'        // Avro pulls in slf4j 1.5 which is API incompatible with 1.6
  }

  implementation('org.apache.helix:helix-core:1.4.1:jdk8') {
    exclude group: 'org.apache.helix'
  }
  implementation('org.apache.helix:helix-common:1.4.1:jdk8')  {
    exclude group: 'org.apache.helix'
  }
  implementation('org.apache.helix:zookeeper-api:1.4.1:jdk8') {
    exclude group: 'org.apache.helix'
  }
  implementation('org.apache.helix:metadata-store-directory-common:1.4.1:jdk8') {
    exclude group: 'org.apache.helix'
  }
  implementation('org.apache.helix:metrics-common:1.4.1:jdk8')

  implementation (project(':internal:alpini:netty4:alpini-netty4-base')) { // for SSL Initializer
    exclude group: 'com.oracle', module: 'ojdbc14' // unused transitive dependency, doesn't exist in repo
    exclude group: 'xerces', module: 'xerces' // resolve conflicting transitive dependency
    exclude group: 'com.fasterxml.jackson.core', module: 'jackson-core'// resolve conflicting transitive dependency
    exclude group: 'com.google.guava', module: 'guava' // Keeping' it clean.
    exclude group: 'com.github.rholder', module: 'guava-retrying' // Keeping' it clean.
    exclude group: 'com.linkedin.container', module: 'container-eventbus-factory' // Keeping' it clean.
  }

  implementation project(':clients:venice-thin-client')

  implementation libraries.avroUtilCompatHelper
  implementation libraries.bouncyCastle
  implementation libraries.caffeine
  implementation libraries.classgraph
  implementation libraries.commonsCodec
  implementation libraries.commonsIo // IntelliJ gets confused when running tests unless we explicitly depend on a recent version of commons-io
  implementation libraries.commonsLang
  implementation libraries.conscrypt
  implementation libraries.fastUtil
  implementation libraries.httpAsyncClient
  implementation libraries.jacksonDatabind
  implementation libraries.jacksonAnnotations
  implementation libraries.kafkaClients // TODO: Get rid of Kafka dependency in venice-common
  implementation libraries.oss
  implementation libraries.tehuti
  // It's necessary to pull in the most recent version of zkclient explicitly, otherwise Helix won't have it...
  implementation libraries.zkclient
  implementation libraries.zookeeper // TODO: Get rid of ZK dependency in venice-common

  // Test dependencies are allowed to be bulkier.
  testImplementation libraries.kafkaClientsTest // TODO: Get rid of Kafka dependency in venice-common (used by TopicCreator)
  testImplementation project(':internal:alpini:common:alpini-common-base')
  testImplementation project(':internal:alpini:common:alpini-common-io')
  testImplementation project(':internal:alpini:router:alpini-router-api')
  testImplementation project(':services:venice-controller')
  testImplementation project(':clients:da-vinci-client')
}

task generateSslCertificate(type: Exec) {
  def source = file('./create-ssl-cert.sh')
  def outputDir = file("$buildDir/generated/resources")
  inputs.file(source)
  outputs.dir(outputDir)
  commandLine = [source, outputDir]
}
sourceSets.main.resources.srcDir(generateSslCertificate)

ext {
  jacocoCoverageThreshold = 0.33
}
