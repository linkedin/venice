import groovy.json.JsonSlurper
import groovy.json.JsonOutput
import java.nio.file.Files
import java.nio.file.Path
import java.nio.file.Paths
import java.nio.file.StandardCopyOption
import java.nio.file.StandardOpenOption


apply {
  plugin 'me.champeau.jmh'
}

jmh {
  fork = 3
  warmupForks = 1
  iterations = 10
  warmupIterations = 5
  timeUnit = 'ns'
  resultFormat = 'json'
  failOnError = true
  includeTests = false
  profilers = ['gc']
  benchmarkMode = ['sample']
  includes = ['DaVinciClientBenchmark']
  jvmArgs = ['-Xms4G', '-Xmx4G', '-Djmh.shutdownTimeout=0', '-Djmh.shutdownTimeout.step=0']
}

jmhJar {
  zip64 = true
}

configurations {
  all {
    resolutionStrategy {
      force libraries.kafka
      force libraries.javax
    }
  }
  implementation {
    exclude group: 'org.mortbay.jetty', module: 'servlet-api'
  }
  integrationTestImplementation.extendsFrom testImplementation
  integrationTestUtils
}

sourceSets {
  integrationTest {
    // 'src/integrationTest/java' is in srcDir by default. Just add the proto directory
    java.srcDir 'src/integrationTest/proto'
    resources
  }
  jmh {
    // 'src/jmh/java' is in srcDir by default. Just add the proto directory
    java.srcDir 'src/jmh/proto'
    resources
  }
}

dependencies {
  implementation (libraries.d2) {
    exclude group: 'com.oracle', module: 'ojdbc14' // unused transitive dependencies, doesn't exist in repo
    // this will introduce another different mockito-all version
    exclude group: 'org.mockito', module: 'mockito-all'
  }
  implementation project(':clients:da-vinci-client')
  implementation project(':clients:venice-client')
  implementation project(':clients:venice-push-job')
  implementation project(':internal:venice-common')
  implementation project(':services:venice-controller')
  implementation project(':services:venice-router')
  implementation project(':integrations:venice-samza')
  implementation project(':clients:venice-producer')
  implementation project(':internal:venice-client-common')
  implementation project(':services:venice-server')
  implementation project(':clients:venice-thin-client')
  implementation project(':internal:alpini:netty4:alpini-netty4-base')
  implementation project(':internal:alpini:router:alpini-router-api')
  implementation project(':internal:alpini:router:alpini-router-base')

  implementation('org.apache.helix:helix-core:1.4.1:jdk8') {
    exclude group: 'org.apache.helix'
  }
  implementation('org.apache.helix:helix-common:1.4.1:jdk8')  {
    exclude group: 'org.apache.helix'
  }
  implementation('org.apache.helix:zookeeper-api:1.4.1:jdk8') {
    exclude group: 'org.apache.helix'
  }
  implementation('org.apache.helix:metadata-store-directory-common:1.4.1:jdk8') {
    exclude group: 'org.apache.helix'
  }
  implementation('org.apache.helix:metrics-common:1.4.1:jdk8')

  implementation libraries.avroUtilCompatHelper
  implementation libraries.avroUtilFastserde
  implementation libraries.commonsCli
  implementation libraries.conscrypt
  implementation libraries.fastUtil
  implementation (libraries.hadoopCommon) {
    exclude group: 'javax.servlet'
  }
  implementation libraries.httpAsyncClient
  implementation libraries.javax
  implementation libraries.kafka
  implementation libraries.kafkaClients
  implementation libraries.kafkaClientsTest
  implementation libraries.mockito
  implementation libraries.rocksdbjni
  implementation libraries.samzaApi
  implementation libraries.spark
  implementation libraries.testng
  implementation libraries.zstd
  implementation libraries.openTelemetryTestSdk

  implementation (libraries.mapreduceClientJobClient) {
    exclude group: 'org.apache.avro'
    exclude group: 'javax.servlet'
  }
  testImplementation project(':clients:venice-admin-tool')
  testImplementation project(':internal:alpini:common:alpini-common-base')
  testImplementation project(':internal:venice-common').sourceSets.test.output
  testImplementation libraries.log4j2core
  testImplementation libraries.log4j2api
  testImplementation libraries.kafkaClients

  jmhAnnotationProcessor 'org.openjdk.jmh:jmh-generator-annprocess:' + jmh.jmhVersion.get()
  jmhImplementation project(path: ':internal:venice-test-common', configuration: 'integrationTestUtils')
}

def integrationTestConfigs = {
  mustRunAfter test
  classpath = sourceSets.integrationTest.runtimeClasspath
  testClassesDirs = sourceSets.integrationTest.output.classesDirs
  forkEvery = Integer.parseInt(project.properties.get('integrationTest.forkEvery', "$forkEvery"))
  maxParallelForks = Integer.parseInt(project.properties.get('integrationTest.maxParallelForks', "$maxParallelForks"))

  // Set heap size to 8GB for integration tests
  minHeapSize = '4g'
  maxHeapSize = '8g'

  // Disable JaCoCo for integration tests
  jacoco {
    enabled = false
  }
}

def shardAssignmentsFile = new File(projectDir, "test-shard-assignments.json")
def shardAssignments = new JsonSlurper().parse(shardAssignmentsFile)
def integrationTestBuckets = shardAssignments.shards as Map<String, List<String>>

integrationTestBuckets.each { name, patterns ->
  task "integrationTests_${name}" (type: Test) {
    ext {
      suiteStartTime = 0
      timingResults = []
    }
    filter {
      patterns.each { pattern ->
        includeTestsMatching pattern
      }
    }
    configure integrationTestConfigs
    useTestNG {
      excludeGroups 'flaky'
      listeners = ['com.linkedin.venice.testng.VeniceSuiteListener', 'com.linkedin.venice.testng.VeniceTestListener']
    }
    beforeSuite { descriptor ->
      suiteStartTime = System.currentTimeMillis()
    }
    afterSuite { descriptor, result ->
      if (descriptor.name.startsWith("com.linkedin")) {
        def durationSeconds = (System.currentTimeMillis() - suiteStartTime) / 1000.0
        println "Test Suite ${descriptor.name} completed (${result.getResultType()}) in ${durationSeconds} s"
        timingResults << [name: descriptor.name, durationSeconds: durationSeconds, result: result.getResultType().toString()]
      }
    }
    doLast {
      def timingDir = new File(buildDir, "test-timings")
      timingDir.mkdirs()
      def timingFile = new File(timingDir, "integrationTests_${name}.json")
      timingFile.text = JsonOutput.prettyPrint(JsonOutput.toJson([shard: name, suites: timingResults]))
    }
  }
}

task integrationTests_99(type: Test) {
  ext {
    suiteStartTime = 0
    timingResults = []
  }
  filter {
    integrationTestBuckets.each { name, patterns ->
      patterns.each { pattern ->
        excludeTestsMatching pattern
      }
    }
  }
  beforeSuite { descriptor ->
    suiteStartTime = System.currentTimeMillis()
  }
  configure integrationTestConfigs
  useTestNG {
    excludeGroups 'flaky'
    listeners = ['com.linkedin.venice.testng.VeniceSuiteListener', 'com.linkedin.venice.testng.VeniceTestListener']
  }
  afterSuite { descriptor, result ->
    if (descriptor.name.startsWith("com.linkedin")) {
      def durationSeconds = (System.currentTimeMillis() - suiteStartTime) / 1000.0
      println "Test Suite ${descriptor.name} completed (${result.getResultType()}) in ${durationSeconds} s"
      timingResults << [name: descriptor.name, durationSeconds: durationSeconds, result: result.getResultType().toString()]
    }
  }
  doLast {
    def timingDir = new File(buildDir, "test-timings")
    timingDir.mkdirs()
    def timingFile = new File(timingDir, "integrationTests_99.json")
    timingFile.text = JsonOutput.prettyPrint(JsonOutput.toJson([shard: "99", suites: timingResults]))
  }
}

def getFileContent(String targetDir, String fileName) {
  def file = new File(targetDir, fileName)
  def filePath = Paths.get(file.getPath())
  return new String(Files.readAllBytes(filePath))
}

task generateGHCI() {
  def targetDir = rootDir.getPath() + "/.github/rawWorkflows/"
  def targetFile = new File(targetDir, "VeniceCI-E2ETests.yml")
  def targetFilePath = Paths.get(targetFile.getPath())

  def paramFileContent = getFileContent(targetDir, "gh-ci-parameterized-flow.txt")
  def completionFileContent = getFileContent(targetDir, "gh-ci-completion-flow.txt")

  targetFile.delete()
  targetFile.createNewFile()

  append(targetFilePath, "# Auto-generated file. Do not edit manually!\n#\n")
  append(targetFilePath, "# To alter these flows, edit:\n#\n#     internal/venice-test-common/build.gradle\n#\n")
  append(targetFilePath, "# To regenerate, run:\n#\n#     ./gradlew generateGHCI\n\n")

  append(targetFilePath, "name: E2ETests\n\n")
  append(targetFilePath, "on: [push, pull_request, workflow_dispatch]\n\n")
  append(targetFilePath, "jobs:\n")

  def jobs = []

  def common = "--continue --no-daemon "

  def integTestGradleArgs = common + "-DforkEvery=1 -DmaxParallelForks=1 integrationTests_"
  def integrationTestTimeout = 15 // minutes
  integrationTestBuckets.each { name, patterns ->
    def flowName = "IntegrationTests_" + name
    jobs << flowName
    appendToGHCI(paramFileContent, targetFilePath, flowName, integrationTestTimeout, integTestGradleArgs + name)
  }
  def otherTest = "integrationTests_99"
  appendToGHCI(paramFileContent, targetFilePath, otherTest, integrationTestTimeout, integTestGradleArgs + "99")
  jobs << otherTest

  // define a job that depends others to manage the status check
  appendToGHCI(completionFileContent, targetFilePath, "E2ETestsFailureAlert", 20, "null", jobs)

  def finalDestinationPath = Paths.get(rootDir.getPath() + "/.github/workflows/VeniceCI-E2ETests.yml")
  Files.move(targetFilePath, finalDestinationPath, StandardCopyOption.REPLACE_EXISTING)
}

def appendToGHCI(String paramFileContent, Path targetFilePath, String flowName, int timeOut, String gradleArguments, ArrayList dependency=null) {
  String postProcessing = paramFileContent
      .replace("\$FlowName", flowName)
      .replace("\$TimeOut", Integer.toString(timeOut))
      .replace("\$GradleArguments", gradleArguments)

  if (dependency == null) {
    postProcessing = postProcessing.replace("    needs: \$Dependency\n", "")
  } else {
    postProcessing = postProcessing.replace("\$Dependency", dependency.toString())
  }

  append(targetFilePath, postProcessing)
  append(targetFilePath, "\n")
}

def append(Path targetFilePath, String content) {
  Files.write(targetFilePath, content.getBytes(), StandardOpenOption.APPEND)
}

task integrationTest(type: Test) {
  configure integrationTestConfigs
}
check.dependsOn(integrationTest)

flakyTest {
  classpath += sourceSets.integrationTest.runtimeClasspath
  testClassesDirs += sourceSets.integrationTest.output.classesDirs
}

idea {
  module {
    testSourceDirs += project.sourceSets.integrationTest.java.srcDirs
    testSourceDirs += project.sourceSets.jmh.java.srcDirs

    testResourceDirs += project.sourceSets.integrationTest.resources.srcDirs
  }
}

task integrationTestJar(type: Jar) {
  classifier 'integrationTest'
  from sourceSets.integrationTest.output
}

artifacts {
  integrationTestUtils integrationTestJar
}

ext {
  jacocoCoverageThreshold = 0.00
  diffCoverageThreshold = 0.00
}

publishing {
  publications {
    "${project.name}" (MavenPublication) {
      artifact integrationTestJar
    }
  }
}
