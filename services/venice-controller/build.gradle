plugins {
  id 'application'
  id 'com.github.johnrengelman.shadow'
}

application {
  mainClassName = 'com.linkedin.venice.controller.VeniceController'
}
installDist.dependsOn test

//the mortbay jetty will introduce servlet 2.5 which will cause the NoSuchMethodException while admin spark server is trying to read head from request.
configurations.implementation {
  exclude group: 'org.mortbay.jetty', module: 'servlet-api'
}

dependencies {
  implementation (libraries.d2) {
    exclude group: 'com.oracle', module: 'ojdbc14' // unused transitive dependencies, doesn't exist in repo
    exclude group: 'org.mockito', module: 'mockito-all' // this will introduce another different mockito-all version
  }
  implementation('org.apache.helix:helix-core:1.4.1:jdk8') {
    exclude group: 'org.apache.helix'
  }
  implementation('org.apache.helix:helix-common:1.4.1:jdk8')  {
    exclude group: 'org.apache.helix'
  }
  implementation('org.apache.helix:zookeeper-api:1.4.1:jdk8') {
    exclude group: 'org.apache.helix'
  }
  implementation('org.apache.helix:metadata-store-directory-common:1.4.1:jdk8') {
    exclude group: 'org.apache.helix'
  }
  implementation('org.apache.helix:metrics-common:1.4.1:jdk8')

  implementation project(':internal:venice-common')
  implementation project(':clients:venice-thin-client')

  implementation libraries.commonsIo
  implementation libraries.fastUtil
  implementation libraries.httpAsyncClient
  implementation libraries.kafkaClients
  implementation libraries.log4j2api
  implementation libraries.spark
  // It's necessary to pull in the most recent version of zkclient explicitly, otherwise Helix won't have it...
  implementation libraries.zkclient

  testImplementation project(':services:venice-router')
  testImplementation libraries.avroUtilFastserde
  testImplementation libraries.kafkaClientsTest // TODO: Get rid of Kafka dependency in venice-common (used by TopicCreator)
  testImplementation project(':internal:venice-test-common')
}

jar {
  manifest {
    attributes = [
        'Implementation-Title': 'Venice Controller',
        'Implementation-Version': project.version,
        'Main-Class': application.mainClass.get()
    ]
  }
}

ext {
  jacocoCoverageThreshold = 0.21
}
