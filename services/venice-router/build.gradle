plugins {
  id 'application'
  id 'com.github.johnrengelman.shadow'
}

application {
  mainClassName = 'com.linkedin.venice.router.RouterServer'
}
installDist.dependsOn test

configurations.all {
  // Router dependencies import from "org.jboss.netty" and we want them to use "io.netty"
  resolutionStrategy.eachDependency { DependencyResolveDetails details ->
    if (details.requested.group == 'org.jboss.netty') {
      details.useTarget group: 'io.netty', name: details.requested.name, version: '3.9.6.Final'
    }
  }
}

dependencies {
  implementation project(':internal:venice-common')

  implementation (project(':internal:alpini:common:alpini-common-base'))  {
    exclude group: 'javax.servlet'
    exclude group: 'org.mockito', module: 'mockito-all'
  }

  implementation (project(':internal:alpini:router:alpini-router-api'))  {
    exclude group: 'javax.servlet'
    exclude group: 'org.mockito', module: 'mockito-all'
  }

  implementation (project(':internal:alpini:router:alpini-router-base'))  {
    exclude group: 'javax.servlet'
    exclude group: 'org.mockito', module: 'mockito-all'
  }

  implementation (project(':internal:alpini:netty4:alpini-netty4-base')) { // for SSL Initializer
    exclude group: 'com.oracle', module: 'ojdbc14' // unused transitive dependency, doesn't exist in repo
    exclude group: 'xerces', module: 'xerces' // resolve conflicting transitive dependency
    exclude group: 'com.fasterxml.jackson.core', module: 'jackson-core'// resolve conflicting transitive dependency
  }

  implementation (libraries.d2) {
    exclude group: 'com.oracle', module: 'ojdbc14' // unused transitive dependencies, doesn't exist in repo
    exclude group: 'org.mockito', module: 'mockito-all' // this will introduce another different mockito-all version
  }

  implementation libraries.fastUtil

  implementation('org.apache.helix:helix-core:1.4.1:jdk8') {
    exclude group: 'org.apache.helix'
  }
  implementation('org.apache.helix:helix-common:1.4.1:jdk8')  {
    exclude group: 'org.apache.helix'
  }
  implementation('org.apache.helix:zookeeper-api:1.4.1:jdk8') {
    exclude group: 'org.apache.helix'
  }
  implementation('org.apache.helix:metadata-store-directory-common:1.4.1:jdk8') {
    exclude group: 'org.apache.helix'
  }
  implementation('org.apache.helix:metrics-common:1.4.1:jdk8')

  implementation project(':internal:alpini:netty4:alpini-netty4-base')
  implementation libraries.httpAsyncClient
  implementation project(':internal:alpini:router:alpini-router-api')
  implementation project(':internal:alpini:router:alpini-router-impl')
  implementation libraries.opentelemetryApi

  testImplementation project(':clients:venice-thin-client')
  testImplementation libraries.kafkaClientsTest // TODO: Get rid of Kafka dependency in venice-common (used by TopicCreator)
  testImplementation libraries.kafkaClients
}

jar {
  manifest {
    attributes = [
        'Implementation-Title': 'Venice Router',
        'Implementation-Version': project.version,
        'Main-Class': application.mainClass.get()
    ]
  }
}

ext {
  jacocoCoverageThreshold = 0.40
}

checkerFramework {
  extraJavacArgs = ['-Xmaxerrs', '256']
  checkers = ['org.checkerframework.checker.nullness.NullnessChecker']
  skipCheckerFramework = true
  excludeTests = true
}