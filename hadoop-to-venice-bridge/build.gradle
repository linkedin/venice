dependencies {
    compile project(':venice-common')
    compile project(':venice-client')

    compile libraries.commonsIo
    compile libraries.kafkaClients
    compile libraries.kafka
    compile (libraries.avro) {
      exclude group: 'org.slf4j'        // Avro pulls in slf4j 1.5 which is API incompatible with 1.6
    }

    compile (libraries.mapreduceClientCore) {
        exclude group: 'org.apache.avro'
        exclude group: 'javax.servlet'
    }

    // gradle dependencyInsight is not showing any dependency on the slf4j-simple
    // But if this version is not forced, it causes the slf4j to fail with NoSuchMethodError
    compile 'org.slf4j:slf4j-simple:1.6.2'

    compile libraries.log4j
    compile(libraries.hadoopCommon) {
      // Exclude transitive dependency
      exclude group: 'org.apache.avro'
      exclude group: 'javax.servlet'
    }
    compile libraries.metricsCore
    compile libraries.joptSimple
    compile libraries.jdom
    compile libraries.jacksonCore

    compile libraries.xerces
    compile libraries.xalan

    testCompile project(':venice-client')
    testCompile project(':venice-common').sourceSets.test.output
    testCompile project(':venice-controller')
    testCompile project(':venice-server')
    testCompile project(':venice-router')
    testCompile (libraries.mapreduceClientJobClient) {
        exclude group: 'org.apache.avro'
        exclude group: 'javax.servlet'
    }

    runtime libraries.commonsConfiguration
    runtime libraries.commonsLang
    runtime libraries.httpCore
    runtime libraries.httpClient
}

configurations.all {
  resolutionStrategy {
    // Kafka expects 1.6.2 dependency, but via avro it ends up with 1.5 version which has an API that kafka uses removed.
    // Dependency tree does not show dependency on slf4j-simple 1.5, but slf4j is looking at the jar contents
    // and arrives at the conclusion that it needs 1.5
    // https://docs.gradle.org/current/dsl/org.gradle.api.artifacts.ResolutionStrategy.html#org.gradle.api.artifacts.ResolutionStrategy:force(java.lang.Object[])

    force 'org.slf4j:slf4j-log4j12:1.6.2', 'org.slf4j:slf4j-api:1.6.2'
  }
}


jar {
  // including all runtime dependency jars (fat jar)
  from configurations.runtime.collect {
       // filtering hadoop core jars and configs as it clashes with Azkaban
       it.isDirectory() ? it : zipTree(it).matching {
         exclude("**/*-default.xml")
         exclude("**/org/apache/commons/**")
         exclude("**/org/apache/hadoop/**")
         exclude("**/com/linkedin/azkaban**")
         exclude("**/com/linkedin/hadoop**")
         exclude("hdfs*.xml")
         exclude("mapred*.xml")
         exclude("core*.xml")
         exclude("**/org.apache.hadoop*")
       }
  }

  // also include product-spec for parsing library versions at run time
  // from "../product-spec.json"
  // override log4j properties for hadoop job
  from "log4j.properties"
}

task zipAzkabanDev(type: Zip, dependsOn: jar) {
  classifier = 'azkaban-dev'

  from "azkaban-dev-pack/"
  include '*'

  from (tasks.jar.archivePath) {
    into 'libs'
  }
}

test {
    useTestNG()
}

