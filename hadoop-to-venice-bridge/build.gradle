plugins {
  id 'com.github.johnrengelman.shadow'
}

dependencies {
  implementation (project(':venice-common')) {
    exclude group: 'com.linkedin.dds-storage-core'
  }
  implementation project(':venice-thin-client') // Needed by the Post Bulk-load Analysis Job
  implementation libraries.commonsIo
  implementation libraries.kafkaClients
  implementation libraries.kafka
  implementation (libraries.avro) {
    exclude group: 'org.mortbay.jetty' // jetty 6 conflicts with spark-java used in controller api
  }
  implementation (libraries.avroMapred) {
    exclude group: 'org.mortbay.jetty'
  }
  implementation (libraries.mapreduceClientCore) {
    exclude group: 'org.apache.avro'
    exclude group: 'javax.servlet'
  }
  implementation libraries.log4j2api
  implementation libraries.log4j2core
  implementation (libraries.hadoopCommon) {
    // Exclude transitive dependency
    exclude group: 'org.apache.avro'
    exclude group: 'javax.servlet'
  }
  implementation libraries.joptSimple
  implementation libraries.jdom
  implementation libraries.jacksonCore
  implementation libraries.xerces
  implementation libraries.xalan
  implementation libraries.zstd

  runtimeOnly libraries.commonsConfiguration
  runtimeOnly libraries.commonsLang
  runtimeOnly libraries.httpCore
  runtimeOnly libraries.httpClient
}

artifacts {
  archives shadowJar
}

shadowJar {
  include '**'
  exclude '**/com/linkedin/azkaban**'
  exclude '**/com/linkedin/hadoop**'
  exclude '**/org.apache.hadoop*'
  exclude '**/org/apache/commons/**'
  exclude '**/org/apache/hadoop/**'
  exclude '**/org/apache/xerces/**'
}

task zipAzkabanDev(type: Zip, dependsOn: jar) {
  classifier = 'azkaban-dev'

  from 'azkaban-dev-pack/' include '*'

  from (tasks.jar.archivePath) {
    into 'libs'
  }
}
