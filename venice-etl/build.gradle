apply plugin: 'java'

dependencies {
    compile project(':venice-common')

    compile (libraries.avro) {
        exclude group: 'org.mortbay.jetty' // jetty 6 conflicts with spark-java used in controller api
    }
    compile libraries.gobblinCoreBase
    compile libraries.gobblinKafkaCommon
    compile libraries.guava
}

configurations {
    jarRuntime {
        description = 'this configuration will include all the dependencies to generate the fat jar'
        extendsFrom runtime
    }
    // filtering all the container related classes since they could conflict with customers' job dependencies
    jarRuntime.exclude group: 'com.linkedin.container'
}

configurations.all {
    resolutionStrategy {
        // Kafka expects 1.6.2 dependency, but via avro it ends up with 1.5 version which has an API that kafka uses removed.
        // Dependency tree does not show dependency on slf4j-simple 1.5, but slf4j is looking at the jar contents
        // and arrives at the conclusion that it needs 1.5
        // https://docs.gradle.org/current/dsl/org.gradle.api.artifacts.ResolutionStrategy.html#org.gradle.api.artifacts.ResolutionStrategy:force(java.lang.Object[])

        force 'org.slf4j:slf4j-log4j12:1.6.2'
    }
}

jar {
    zip64 true
    // including all jar runtime dependency jars (fat jar)
    // from configurations.jarRuntime.collect {
    from configurations.jarRuntime
    // from configurations.compile
        .filter {!it.absolutePath.endsWith(".xml")}
        .filter {!it.absolutePath.endsWith(".ivy")} // Exclude the ivy file which is introduced by new helix version.
        .collect {
        // filtering hadoop core jars and configs as it clashes with Azkaban
        it.isDirectory() ? it : zipTree(it).matching {
            // exclude("**/*-default.xml")
            // exclude("**/*.xml")
            exclude("**/org/apache/commons/**")
            exclude("**/org/apache/hadoop/**")
            exclude("**/com/linkedin/azkaban**")
            exclude("**/com/linkedin/hadoop**")
            exclude("**/org/apache/xerces/**")
            exclude("com/google/common/**")
            exclude("org/apache/avro/**")
            // exclude("*.xml")
            // exclude("hdfs*.xml")
            // exclude("mapred*.xml")
            // exclude("core*.xml")
            exclude("**/org.apache.hadoop*")
        }
    }

    // override log4j properties for hadoop job
    from "log4j.properties"
    exclude 'META-INF/*.RSA', 'META-INF/*.SF', 'META-INF/*.DSA'
}