task buildJar(type: Jar) {
  manifest {
    attributes 'Implementation-Title': 'Venice Test Common',
        'Implementation-Version': version
  }
  baseName = project.name
  from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
  with jar
}

configurations.compile {
  exclude group: "org.apache.kafka"
  exclude group: 'org.mortbay.jetty', module:'servlet-api'
}

configurations.all {
  resolutionStrategy {
    force libraries.kafka
  }
}

jar {
  manifest {
    attributes 'Implementation-Title': 'Venice Test Common',
        'Implementation-Version': version
  }
}

sourceSets {
  integrationtest {
    java {
      compileClasspath += main.output + test.output
      runtimeClasspath += main.output + test.output
      srcDir file("src/integrationtest/java")
    }
    resources.srcDir file("src/integrationtest/resources")
  }
}

configurations {
  integrationtestCompile.extendsFrom testCompile
  integrationtestRuntime.extendsFrom testRuntime

}

task integrationtest(type: Test) {
  String forkEveryConfigName = "integrationTest.forkEvery"
  if (project.hasProperty(forkEveryConfigName)) {
    println "$forkEveryConfigName set to ${project.getProperty(forkEveryConfigName)}"
    // Can help isolation of tests
    forkEvery = project.getProperty(forkEveryConfigName)
  } else {
    println forkEveryConfigName + ' not set. Will recycle the same JVM(s).'
  }

  description = "Run integration tests for " + project.name;
  useTestNG()
  testClassesDir = sourceSets.integrationtest.output.classesDir
  classpath = sourceSets.integrationtest.runtimeClasspath
  testReportDirName = "integrationtest"
  testResultsDirName = "integrationtest-result"
  jacoco {
    append = false
    destinationFile = file("$buildDir/jacoco/integrationtest.exec")
  }

  String maxParallelForksConfigName = "integrationTest.maxParallelForks"
  if (project.hasProperty(maxParallelForksConfigName)) {
    println "$maxParallelForksConfigName set to ${project.getProperty(maxParallelForksConfigName)}"
    // Can help isolation of tests
    maxParallelForks = project.getProperty(maxParallelForksConfigName)
  } else {
    maxParallelForks = 10
    println maxParallelForksConfigName + ' not set. Will default to 10 parallel forks.'
  }

  testLogging {
    events "started", "failed", "passed", "skipped"
    exceptionFormat "full"
    showStandardStreams = false // to mute the DDS Router's noisy behavior...
  }
}

task jacocoIntegrationTestReport(type:JacocoReport){
  dependsOn integrationtest
  executionData file("$buildDir/jacoco/integrationtest.exec")
  reports {
    xml.enabled false
    csv.enabled false
    html.destination "${buildDir}/jacocoIntegrationHtml"
  }
  sourceSets sourceSets.main
}

dependencies {
  compile libraries.testng

  compile (libraries.brooklinTest) {
    exclude group: 'org.mockito' //brooklin tries to pull in Mockito 1, we depend on Mockito 2
    exclude group: 'org.apache.kafka' //brooklin tries to pull in Kafka 0.11, but we have not upgraded to that yet.
    exclude group: 'kafka' //brooklin tries to pull in Kafka 0.11, but we have not upgraded to that yet.
    exclude group: 'com.linkedin.linkedin-kafka-clients' //brooklin tries to pull in Kafka 0.11, but we have not upgraded to that yet.
  }
  compile libraries.brooklinKafkaTransport

  compile project(':venice-schema-common')
  compile project(':venice-common_2.10')
  compile project(':venice-server')
  compile project(':venice-controller')
  compile project(':venice-router')
  compile project(':venice-thin-client')
  compile project(':hadoop-to-venice-bridge')

  compile libraries.mockito
  compile project(':venice-brooklin')
  compile project(':venice-samza')
  compile libraries.kafka
  compile libraries.kafkaClients
  compile libraries.kafkaTest
  compile libraries.kafkaClientsTest

  runtime libraries.commonsConfiguration
  runtime libraries.commonsLang
  runtime libraries.httpCore
  runtime libraries.httpClient

  testCompile (libraries.mapreduceClientJobClient) {
    exclude group: 'org.apache.avro'
    exclude group: 'javax.servlet'
  }
}
