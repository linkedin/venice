plugins {
  id 'com.github.johnrengelman.shadow'
}

dependencies {
  implementation(libraries.avro) {
    exclude group: 'org.mortbay.jetty' // jetty 6 conflicts with spark-java used in controller api
  }

  implementation project(':internal:venice-common')
  implementation project(':services:venice-controller')
  implementation project(':clients:venice-thin-client')

  implementation libraries.commonsCli
  implementation libraries.kafka
  implementation libraries.log4j2api
  implementation libraries.log4j2core
  implementation('org.apache.helix:helix-core:1.4.1:jdk8') {
    exclude group: 'org.apache.helix'
  }
  implementation('org.apache.helix:helix-common:1.4.1:jdk8')  {
    exclude group: 'org.apache.helix'
  }
  implementation('org.apache.helix:zookeeper-api:1.4.1:jdk8') {
    exclude group: 'org.apache.helix'
  }
  implementation('org.apache.helix:metadata-store-directory-common:1.4.1:jdk8') {
    exclude group: 'org.apache.helix'
  }
  implementation('org.apache.helix:metrics-common:1.4.1:jdk8')
  implementation libraries.zstd

  testImplementation project(':internal:venice-common').sourceSets.test.output
}

apply from: "$rootDir/gradle/helper/publishing.gradle"
apply from: "$rootDir/gradle/helper/packaging.gradle"

jar {
  manifest {
    attributes = ['Implementation-Title'  : 'Venice Admin Tool',
                  'Implementation-Version': project.version,
                  'Main-Class'            : 'com.linkedin.venice.AdminTool']
  }
}

ext {
  jacocoCoverageThreshold = 0.00
}

checkerFramework {
  extraJavacArgs = ['-Xmaxerrs', '256']
  checkers = ['org.checkerframework.checker.nullness.NullnessChecker']
  skipCheckerFramework = true
  excludeTests = true
}
