plugins {
  id 'com.github.johnrengelman.shadow'
}

dependencies {
  api (libraries.avro) {
    exclude group: 'org.jboss.netty' // older version of netty3 causes transitive conflicts with the router
    exclude group: 'org.mortbay.jetty' // jetty 6 conflicts with spark-java used in controller api
    exclude group: 'org.slf4j'        // Avro pulls in slf4j 1.5 which is API incompatible with 1.6
  }

  implementation (libraries.commonsIo) {
    exclude group: 'org.apapche.zookeeper'
  }

  implementation libraries.bouncyCastle
  implementation libraries.httpAsyncClient
  implementation libraries.jacksonCore
  implementation libraries.jacksonDatabind
  implementation libraries.log4j2api
  implementation libraries.zookeeper

  // Schema related dependencies
  implementation project(':internal:venice-client-common')

  testImplementation project(':internal:venice-common')
  testImplementation project(':internal:venice-test-common')
  testImplementation libraries.kafkaClientsTest
  testImplementation libraries.kafkaClients
  testImplementation libraries.netty
}

artifacts {
  archives shadowJar
}

jar {
  manifest {
    attributes = [
        'Implementation-Title': 'Venice Thin Client',
        'Implementation-Version': project.version,
        'Main-Class': 'com.linkedin.venice.client.store.QueryTool'
    ]
  }
}

ext {
  jacocoCoverageThreshold = 0.44
}

checkerFramework {
  extraJavacArgs = ['-Xmaxerrs', '256']
  checkers = ['org.checkerframework.checker.nullness.NullnessChecker']
  skipCheckerFramework = true
  excludeTests = true
}