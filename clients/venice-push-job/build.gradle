plugins {
  id 'com.github.johnrengelman.shadow'
}

dependencies {
  implementation project(':clients:da-vinci-client')
  implementation (project(':internal:venice-common')) {
    exclude module: ':internal:alpini'
  }

  implementation (libraries.avro) {
    exclude group: 'org.mortbay.jetty' // jetty 6 conflicts with spark-java used in controller api
  }

  implementation (libraries.avroMapred) {
    exclude group: 'org.mortbay.jetty'
  }

  implementation (libraries.mapreduceClientCore) {
    exclude group: 'org.apache.avro'
    exclude group: 'javax.servlet'
  }

  implementation (libraries.hadoopCommon) {
    // Exclude transitive dependency
    exclude group: 'org.apache.avro'
    exclude group: 'javax.servlet'
  }

  implementation (libraries.hadoopHdfs) {
    // Exclude transitive dependency
    exclude group: 'org.apache.avro'
    exclude group: 'javax.servlet'
    exclude group: 'com.fasterxml.jackson.core'
  }

  implementation (libraries.apacheSparkAvro) {
    // Spark 3.1 depends on Avro 1.8.2 - which uses avro-mapred with the hadoop2 classifier. Starting from Avro 1.9
    // onwards, avro-mapred is no longer published with a hadoop2 classifier, but Gradle still looks for one.
    exclude group: 'org.apache.avro', module: 'avro-mapred'

    // Spark 3.3 depends on hadoop-client-runtime and hadoop-client-api, which are shaded jars that were added in Hadoop 3.0.3
    exclude group: 'org.apache.hadoop', module: 'hadoop-client-runtime'
    exclude group: 'org.apache.hadoop', module: 'hadoop-client-api'

    // Spark 3.3 depends on Avro 1.11 which is known to be susceptible to a deadlock bug (AVRO-3243)
    exclude group: 'org.apache.avro'

    // Spark 3.3 depends on log4j 2.17.2 which has a performance regression (LOG4J2-3487)
    exclude group: 'org.apache.logging.log4j'
  }
  implementation (libraries.apacheSparkCore) {
    // Spark 3.1 depends on Avro 1.8.2 - which uses avro-mapred with the hadoop2 classifier. Starting from Avro 1.9
    // onwards, avro-mapred is no longer published with a hadoop2 classifier, but Gradle still looks for one.
    exclude group: 'org.apache.avro', module: 'avro-mapred'

    // Spark 3.3 depends on hadoop-client-runtime and hadoop-client-api, which are shaded jars that were added in Hadoop 3.0.3
    exclude group: 'org.apache.hadoop', module: 'hadoop-client-runtime'
    exclude group: 'org.apache.hadoop', module: 'hadoop-client-api'

    // Spark 3.3 depends on Avro 1.11 which is known to be susceptible to a deadlock bug (AVRO-3243)
    exclude group: 'org.apache.avro'

    // Spark 3.3 depends on log4j 2.17.2 which has a performance regression (LOG4J2-3487)
    exclude group: 'org.apache.logging.log4j'
  }
  implementation (libraries.apacheSparkSql) {
    // Spark 3.1 depends on Avro 1.8.2 - which uses avro-mapred with the hadoop2 classifier. Starting from Avro 1.9
    // onwards, avro-mapred is no longer published with a hadoop2 classifier, but Gradle still looks for one.
    exclude group: 'org.apache.avro', module: 'avro-mapred'

    // Spark 3.3 depends on hadoop-client-runtime and hadoop-client-api, which are shaded jars that were added in Hadoop 3.0.3
    exclude group: 'org.apache.hadoop', module: 'hadoop-client-runtime'
    exclude group: 'org.apache.hadoop', module: 'hadoop-client-api'

    // Spark 3.3 depends on Avro 1.11 which is known to be susceptible to a deadlock bug (AVRO-3243)
    exclude group: 'org.apache.avro'

    // Spark 3.3 depends on log4j 2.17.2 which has a performance regression (LOG4J2-3487)
    exclude group: 'org.apache.logging.log4j'
  }

  // Spark versions 3.2.X - 3.3.X are compiled with antlr4 4.8. In our classpath, antlr4 version 4.5 is used. This
  // discrepancy causes errors at runtime.
  implementation libraries.antlr4
  implementation libraries.antlr4Runtime

  // Spark needs log4j-slf4j-impl that got excluded via exclude group: 'org.apache.logging.log4j'
  implementation libraries.log4j2Slf4j

  implementation project(':clients:venice-thin-client') // Needed by the KME SchemaReader

  implementation libraries.commonsIo
  implementation libraries.fastUtil
  implementation libraries.jacksonCore
  implementation libraries.jdom
  implementation libraries.joptSimple
  implementation libraries.kafkaClients
  implementation libraries.log4j2api
  implementation libraries.xalan
  implementation libraries.xerces
  implementation libraries.zstd

  testImplementation project(':internal:venice-common').sourceSets.test.output

  runtimeOnly libraries.commonsConfiguration
  runtimeOnly libraries.commonsLang
  runtimeOnly libraries.httpCore
  runtimeOnly libraries.httpClient
}

apply from: "$rootDir/gradle/helper/publishing.gradle"
apply from: "$rootDir/gradle/helper/packaging.gradle"

jar {
  manifest {
    attributes 'Main-Class': 'com.linkedin.venice.hadoop.VenicePushJob'
  }
}

ext {
  jacocoCoverageThreshold = 0.39
}
