apply plugin: 'application'

mainClassName = "com.linkedin.venice.server.VeniceServer"

installApp.dependsOn test

task buildJar(type: Jar) {
  manifest {
    attributes 'Main-Class': 'com.linkedin.venice.server.VeniceServer',
        'Implementation-Title': 'Venice Server',
        'Implementation-Version': version
  }
  baseName = project.name
  from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
  with jar
}

jar {
  manifest {
    attributes 'Main-Class': 'com.linkedin.venice.server.VeniceServer',
        'Implementation-Title': 'Venice Server',
        'Implementation-Version': version
  }
}

dependencies {
  compile(libraries.helix) {
    // Helix pulls in an ancient version of the zkclient, which lacks some APIs and clashes with Kafka's dependency
    exclude group: 'com.github.sgroschupf'
  }
  compile libraries.zkclient // It's necessary to pull in the most recent version of zkclient explicitly, otherwise Helix won't have it...
  compile libraries.bdbJe
  compile libraries.netty
  compile libraries.asyncHttpClient

  compile project(':venice-common')
  compile project(':venice-controller')

  testCompile project(':venice-client')
  testCompile project(':venice-common').sourceSets.test.output
  testCompile project(':venice-router')
  testCompile 'commons-io:commons-io:2.4'

}


idea {
  module {
    downloadJavadoc = true
    downloadSources = true
  }
}

test {
  useTestNG()
}
