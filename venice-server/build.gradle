apply plugin: 'application'

mainClassName = "com.linkedin.venice.server.VeniceServer"

installDist.dependsOn test

task buildJar(type: Jar) {
  zip64 true
  manifest {
    attributes 'Main-Class': 'com.linkedin.venice.server.VeniceServer',
        'Implementation-Title': 'Venice Server',
        'Implementation-Version': version
  }
  baseName = project.name
  from { configurations.implementation.collect { it.isDirectory() ? it : zipTree(it) } }
  with jar
}

jar {
  manifest {
    attributes 'Main-Class': 'com.linkedin.venice.server.VeniceServer',
        'Implementation-Title': 'Venice Server',
        'Implementation-Version': version
  }
}

dependencies {
  implementation (libraries.routerLnkd){ // for SSL Initializer
    exclude group: 'com.oracle', module: 'ojdbc14' // unused transitive dependency, doesnt exist in repo
    exclude group: 'xerces', module: 'xerces' // resolve conflicting transitive dependency
    exclude group: 'com.fasterxml.jackson.core', module: 'jackson-core'// resolve conflicting transitive dependency
  }
  implementation libraries.zkclient // It's necessary to pull in the most recent version of zkclient explicitly, otherwise Helix won't have it...
  implementation libraries.bdbJe
  implementation libraries.helix
  implementation libraries.netty
  implementation libraries.asyncHttpClient
  implementation libraries.tehuti
  implementation libraries.commonsIo
  implementation (libraries.netty4Lnkd){
    exclude group: 'javax.servlet'
    exclude group: 'org.mockito', module: 'mockito-all'
  }
  implementation libraries.rocksdbjni
  implementation libraries.linkedinAvroFastserde

  implementation project(':venice-common_2.10')
  implementation project(':venice-controller')
  implementation (project(':venice-thin-client')){
    exclude group: 'org.jboss.netty' // venice-common's exclusion of netty 3.2.1 isn't respected
  }

  testCompile (project(':venice-router')){
    exclude group: 'org.jboss.netty' // venice-common's exclusion of netty 3.2.1 isn't respected
  }
  testCompile project(':venice-brooklin')

  testCompile 'commons-io:commons-io:2.4'
  testCompile libraries.tehuti

  testCompile project(':venice-test-common_2.10')
  testCompile project(':venice-router')
}
